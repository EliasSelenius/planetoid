
struct Entity {
    Planet* planet;
    Transform2D tr;
    DrawBuffers db;
    vec2 vel;
}

struct Planet {
    VoxelGrid grid;
    Transform2D tr;

    float32 radius;
    float32 orbit_dist;
    float32 orbit_duration;
    float32 orbit_phase;
}


const entities_capacity = 256;
int32 entities_count = 0;
Entity* entities;
Entity* player;


const planets_count = 9;
Planet* planets;


Entity* appendEntity(DrawBuffers db) {
    if entities_count >= entities_capacity {
        print("Failed to append entity. Max limit reached.");
        return null;
    }

    let res = *entities[entities_count++];
    res.tr.pos = make_vec(0, 0);
    res.tr.scale = 1;
    res.tr.rot = 0;
    res.db = db;
    res.vel = make_vec(0, 0);
    return res;
}

void update_entity(Entity* en) {
    let dt = deltatime();
    en.tr.pos = en.tr.pos + en.vel * dt;

    en.planet = null;
    float32 min_sq_dist = 99999999999;
    for 0 .. planets_count {
        Planet* planet = *planets[it];
        let radius = planet.radius * 2;
        let sq_dist = sqdist(planet.tr.pos, en.tr.pos);
        if sq_dist < radius*radius {
            if sq_dist < min_sq_dist {
                en.planet = planet;
                min_sq_dist = sq_dist;
            }
        }
    }

    if en.planet {
        vec2 diff = en.planet.tr.pos - en.tr.pos;
        let sq_dist = sqlength(diff);
        vec2 down = noz(diff);

        float32 area = Pi * en.planet.radius * en.planet.radius;
        float32 planet_mass = area; // assuming the same uniform mass-density on all planets.
        vec2 gravity = down * planet_mass / sq_dist;
        if sq_dist == 0.0 { gravity = vec2_zero; }
        // en.vel = en.vel + gravity*dt;
        en.vel = en.vel + down*9.8*dt;

        check_collision(en.planet, en);

        float32 floor2multiple(float32 x, n) return floorf(x/n) * n;

        // [ -Pi .. Pi ]
        let target_angle = vec2_to_angle(neg(down));
        // target_angle += floor2multiple(en.tr.rot, Tau);

        // en.tr.rot = fract((en.tr.rot + Pi) / Tau) * Tau - Pi; // normalizes angle to [-Pi, Pi] range


        en.tr.rot = exp_decay(en.tr.rot, target_angle, 3, dt);

        float32 text_h = 0.05;
        draw_text(make_vec(0, 0.02), text_h, to_string((en.tr.rot * rad2deg) as int32), White);
        draw_text(make_vec(0, 0.02 - text_h), text_h, to_string((target_angle * rad2deg) as int32), White);
    }

}

void check_collision(Planet* planet, Entity* en) {

    draw_circle(en.tr.pos, 0.5, make_vec(1,1,1, 0.3));

    Intersection2D inter;
    // if point_intersects(*planet.grid, planet.tr, en.tr.pos, *inter) {
    if circle_intersects(*planet.grid, planet.tr, en.tr.pos, 0.5, *inter) {

        vec2 normal = inter.surface_normal;
        vec2 correction = mul(normal, -inter.distance);
        en.tr.pos = add(en.tr.pos, correction);

        // en.vel = reflect(en.vel, normal); // bounce
        en.vel = add(en.vel, mul(normal, -dot(en.vel, normal))); // cancel velocity in direction of surface
    }
}

DrawBuffers genCircle(int32 res, float32 radius) {
    uint32 vertsCount = res as uint32 + 1;
    vertex2D* verts = malloc(vertsCount * sizeof vertex2D);
    verts[0] = {
        pos = {},
        uv = {},
        color = White
    };

    for i : 1 .. res + 1 {
        float32 angle = i * Tau / res;
        verts[i].pos.x = cosf(angle) * radius;
        verts[i].pos.y = sinf(angle) * radius;

        verts[i].color = White;
    }

    uint32 indsCount = (res as uint32) * 3;
    uint32* inds = malloc(indsCount * sizeof uint32);
    uint32 i = 0;
    uint32 v = 1;
    while i < indsCount {
        inds[i] = 0;
        inds[i+1] = v++;
        inds[i+2] = v;
        i += 3;
    }

    inds[i - 1] = 1;

    let db = create_draw_buffers(sizeof vertex2D, vertex2D_attribs);
    update_buffers(*db, verts, vertsCount, inds, indsCount);
    free(verts);
    free(inds);

    bind_instance_buffer_to_vao(db.vao, draw2d_renderer.vbo_instances);

    return db;
}

void draw_planet(Planet* planet) {

    if planet.grid.must_remesh {
        remesh(*planet.grid);
    }


    Transform2D tr = planet.tr;
    tr.scale = planet.radius * 2;
    draw_texture(
        db_circle,
        tr,
        0,
        white_texture,
        false,
        vec2_zero, vec2_one,
        make_vec(1,1,1, 0.1), vec4_zero);

    draw_texture(
        planet.grid.db,
        planet.tr,
        0,
        white_texture,
        false,
        vec2_zero, vec2_one,
        vec4_one, vec4_zero);
}

Planet generate_planet(float32 radius, float32 dist) {
    Planet p = {};
    p.tr.scale = 1;
    p.radius = radius;
    p.orbit_dist = dist;
    p.orbit_duration = powf(p.orbit_dist, 3.0/2.0) * 0.1;
    p.grid = generate_planet_grid(*p);
    // p.orbit_phase = Pi * random(p.orbit_dist as int32);
    p.orbit_phase = 0;

    return p;
}

VoxelGrid generate_planet_grid(Planet* planet) {
    const radius2gridsize_ratio = 2.5;
    uint32 grid_size = (planet.radius * radius2gridsize_ratio) as uint32;
    VoxelGrid grid = createVoxelGrid(grid_size);

    float32 half = grid.res as float32 / 2;

    for uint32 x : 0 .. grid.res {
        for uint32 y : 0 .. grid.res {
            float32 xf = x as float32 - half;
            float32 yf = y as float32 - half;
            float32 len = sqrtf(xf*xf + yf*yf);
            const scale = 4.0;
            float32 offset = planet.orbit_dist;

            float32 f = 1.0 / len * scale;
            if len == 0.0  f = 0;

            let h = (gnoise(offset + xf * f, offset + yf * f) + 1.0) / 2.0;
            h *= planet.radius / 3.0;
            h = planet.radius - h;

            uint32 i = get_index(grid.res, x, y);
            grid.data[i] = clamp(h - len, 0, 1);

            // grid.data[i] = clamp(40 - len, 0, 1);

            // if len < 40 grid.data[i] = clamp(random(i as int32), 0, 1);
            // else grid.data[i] = 0;
        }
    }

    remesh(*grid);
    return grid;
}

void update_player() {
    float32 dt = deltatime();

    if key('F') { player.vel.x = 0.0; player.vel.y = 0.0; }

    float32 c = cosf(player.tr.rot);
    float32 s = sinf(player.tr.rot);
    // vec2 dir = make_vec(dot(make_vec(c, s), Input.wasd), dot(make_vec(-s, c), Input.wasd));

    mat2 m = { c, s, -s, c };
    vec2 dir = m * Input.wasd;
    player.vel = player.vel + dir*13*dt;

    player.tr.rot += input_axis('Q', 'E') * 0.05;

    // TODO: exponential decay?
    camera.pos = player.tr.pos;
    camera.rot = player.tr.rot;
}

void world_init() {
    camera.rot = 0;
    camera.scale = 400;

    entities = malloc(sizeof(Entity) * entities_capacity);
    planets = malloc(sizeof(Planet) * planets_count);

    for i : 0 .. planets_count {
        float32 r = 30.0 + random(i as int32) * 20;
        Planet p = generate_planet(r, 1000 + 100 * i);
        planets[i] = p;
    }

    player = appendEntity(genCircle(4, 0.1f));
    player.tr.pos = { 1000, planets[0].radius };
}

void world_update() {

    float32 time = app.total_run_time;
    // time += 10000.0;
    time = 0;
    for i : 0 .. planets_count {
        Planet* planet = *planets[i];

        float32 angle = planet.orbit_phase + Tau * time / planet.orbit_duration;
        planet.tr.pos = {cosf(angle), sinf(angle)} * planet.orbit_dist;

        draw_planet(planet);
    }

    // draw_text({0, 0} as vec2, 0.1, make_string("Hello"), White);


    for i : 0 .. entities_count {
        let e = *entities[i];
        update_entity(e);
    }

    update_player();

    for i : 0 .. entities_count {
        let e = *entities[i];

        let tr = e.tr;
        tr.scale = 1.0 / player_texture.width;
        draw_texture(
            draw2d_renderer.quad_db, //e.db,
            tr,
            0,
            player_texture,
            false,
            vec2_zero, vec2_one,
            vec4_one, vec4_zero);
    }

}

vec2 getMouseWorldCoord() {
    float64 cx;
    float64 cy;
    glfwGetCursorPos(app.main_window, *cx, *cy);

    int32 w;
    int32 h;
    glfwGetFramebufferSize(app.main_window, *w, *h);

    float32 x = cx as float32 / w * 2 - 1;
    float32 y = -(cy as float32 / h * 2 - 1);

    x *= w as float32 / h;  // 16.0 / 9.0;

    // float32 c = cosf(camera.rot) * camera.zoom;
    // float32 s = sinf(camera.rot) * camera.zoom;
    // vec2 res;
    // res.x = c*x + s*y + camera.pos.x;
    // res.y = -s*x + c*y + camera.pos.y;
    let res = local2world(camera, x, y);
    return res;
}